plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE' apply false
}


apply plugin: 'base'

group = 'ru.alfabank'
version = '0.0.1-SNAPSHOT'

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'

    dependencies {
        [
                enforcedPlatform('org.springframework.boot:spring-boot-dependencies:2.1.3.RELEASE'),
                platform('org.junit:junit-bom:5.4.1')
        ].each {
            implementation it
            annotationProcessor it
            testAnnotationProcessor it
        }

        implementation 'org.springframework.boot:spring-boot-starter-aop'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'junit:junit:4.12'

        constraints {
            ['org.projectlombok:lombok:1.18.2'].each {
                compileOnly it
                annotationProcessor it
                testAnnotationProcessor it
            }
        }

        //declare dependencies
        ['org.projectlombok:lombok',
         'org.springframework.boot:spring-boot-configuration-processor',
        ].each {
            compileOnly it
            testCompileOnly it
            annotationProcessor it
            testAnnotationProcessor it
        }

        testImplementation 'org.mockito:mockito-core',
                'org.mockito:mockito-junit-jupiter',
                'org.junit.jupiter:junit-jupiter-engine',
                'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
            events 'passed', 'skipped', 'failed'
        }
        reports {
            html.enabled = true
        }
    }
}